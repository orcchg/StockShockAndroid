import com.orcchg.yandexcontest.build.Dependencies
import com.orcchg.yandexcontest.build.Versions

buildscript {
    repositories {
        google()
        mavenCentral()
        maven { url 'https://plugins.gradle.org/m2/' }
        jcenter()
    }
    dependencies {
        classpath(
            Dependencies.AndroidX.navigationSafeArgs,
            Dependencies.Gradle.androidGradle,
            Dependencies.Gradle.ktGradle
        )
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:${Versions.ktStdLib}"
        classpath "org.jlleitschuh.gradle:ktlint-gradle:${Versions.ktlintGradle}"
        classpath "gradle.plugin.de.fuerstenau:BuildConfigPlugin:${Versions.javaBuildConfig}"
        classpath "io.gitlab.arturbosch.detekt:detekt-gradle-plugin:${Versions.detekt}"
        classpath "com.diffplug.spotless:spotless-plugin-gradle:${Versions.spotless}"
    }
}

apply from: "$rootDir/scripts/gradle/project_dependency_graph.gradle"

allprojects {
    apply plugin: Dependencies.Plugin.detekt
    apply plugin: Dependencies.Plugin.ktlint
    apply plugin: Dependencies.Plugin.spotless

    repositories {
        google()
        mavenCentral()
        jcenter()
    }

    ext {
        androidVersionName = "1.0.0"
        androidVersionCode = 1

        testInstrumentationRunner = "androidx.test.runner.AndroidJUnitRunner"
        testApplicationId = 'com.orcchg.yandexcontest.test'
    }

    ktlint {
        version = Versions.ktlint
        outputToConsole = true
        outputColorName = "RED"
        ignoreFailures = false
        enableExperimentalRules = false
        disabledRules = ["final-newline", "import-ordering", "parameter-list-wrapping", "indent"]
        filter {
            exclude("**/generated/**")
            exclude("**/script/**")
            include("**/kotlin/**")
        }
    }

    apply from: "$rootProject.projectDir/scripts/gradle/detekt.gradle"
    apply from: "$rootProject.projectDir/scripts/gradle/spotless.gradle"
}

subprojects {
    afterEvaluate { p ->
        if (p.hasProperty('android')) {
            android {
                buildToolsVersion Versions.buildToolsVersion
                defaultConfig {
                    minSdkVersion Versions.minSdkVersion
                    targetSdkVersion Versions.targetSdkVersion
                    compileSdkVersion Versions.compileSdkVersion

                    versionCode rootProject.ext.androidVersionCode
                    versionName rootProject.ext.androidVersionName
                    testInstrumentationRunner rootProject.ext.testInstrumentationRunner

                    javaCompileOptions {
                        annotationProcessorOptions {
                            arguments = [
                                    "room.schemaLocation"  : "$projectDir/schemas".toString(),
                                    "room.incremental"     : "true",
                                    "room.expandProjection": "true"
                            ]
                        }
                    }
                }

                buildFeatures {
                    buildConfig true
                    viewBinding true
                }

                buildTypes {
                    release {
                        minifyEnabled false
                        proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
                    }
                }

                compileOptions {
                    sourceCompatibility JavaVersion.VERSION_1_8
                    targetCompatibility JavaVersion.VERSION_1_8
                }

                kotlinOptions {
                    jvmTarget = JavaVersion.VERSION_1_8
                }
            }
        }
    }
}
